plugins {
    alias libs.plugins.paperweight.userdev apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
        withSourcesJar()
        // withJavadocJar()
    }

    tasks.withType(Jar).configureEach {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }

        manifest {
            attributes([
                    'Specification-Title'     : mod_name,
                    'Specification-Vendor'    : mod_author,
                    'Specification-Version'   : rootProject.version,
                    'Implementation-Title'    : mod_name,
                    'Implementation-Version'  : rootProject.version,
                    'Implementation-Vendor'   : mod_author,
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Timestamp'               : System.currentTimeMillis(),
                    'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                    'Built-On-Minecraft'      : libs.versions.minecraft.get()
            ])
        }
    }

    repositories {
        mavenCentral()

        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }

    dependencies {
        compileOnly libs.jetbrains.annotations
        implementation libs.findbugs
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.getRelease().set(17)
    }

    tasks.withType(ProcessResources).configureEach {
        def expandProps = [
                "mod_name"                : mod_name,
                "version"                 : version,
                "group"                   : project.group, //Else we target the task's group.

                "minecraft_version"       : libs.versions.minecraft.get(),
                "minecraft_version_range" : libs.versions.mc.range.get(),

                "neo_version"             : libs.versions.neoforge.get(),
                "neo_loader_version_range": libs.versions.neo.loader.range.get(),
                "neo_version_range"       : libs.versions.neo.range.get(),

                "quilt_loader_version"    : libs.versions.quilt.loader.get(),
                "fabric_loader_version"   : libs.versions.fabric.loader.get(),

                "mod_author"              : mod_author,
                "mod_id"                  : mod_id,
                "mod_license"             : mod_license,
                "mod_description"         : project.description
        ]

        inputs.properties(expandProps)

        filesMatching(['pack.mcmeta', '*.mod.json', '*.mixins.json', 'META-INF/mods.toml', 'plugin.yml']) {
            expand expandProps
        }
    }

    // Disables Gradle's custom module metadata from being published to maven. The
    // metadata includes mapped dependencies which are not reasonably consumable by
    // other mod developers.
    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }

    // Activate reproducible builds
    // https://docs.gradle.org/current/userguide/working_with_files.html#sec:reproducible_archives
    tasks.withType(AbstractArchiveTask).configureEach {
        setPreserveFileTimestamps(false)
        setReproducibleFileOrder(true)
    }
}