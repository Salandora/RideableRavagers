plugins {
    alias libs.plugins.quilt.loom
}

base.archivesName = "${mod_name}-quilt-${libs.versions.minecraft.get()}"

def dependencyProjects = [
        project.dependencies.project(path: ":common", configuration: "namedElements")
]

dependencyProjects.forEach {
    project.evaluationDependsOn(it.dependencyProject.path)
}

repositories {
    maven {
        name 'Quilt (Release)'
        url 'https://maven.quiltmc.org/repository/release/'
    }

    maven {
        name 'Quilt (Snapshot)'
        url 'https://maven.quiltmc.org/repository/snapshot/'
        content {
            includeGroup 'org.quiltmc.fabric_api_qsl'
            includeGroupByRegex 'org\\.quiltmc\\.qsl(?:\\.[a-z_]+)?'
        }
    }
}

dependencies {
    minecraft libs.minecraft
    mappings loom.officialMojangMappings()

    modImplementation libs.quilt.loader
    modImplementation libs.quilted.fabric.api

    dependencyProjects.forEach {
        implementation it
        if (it.dependencyProject.hasProperty("sourceSets.client")) {
            clientImplementation it.dependencyProject.sourceSets.client.output
        }
    }
}

loom {
    runtimeOnlyLog4j = true

    if (file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(file("src/main/resources/${mod_id}.accesswidener"))
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
            dependencyProjects.each {
                sourceSet it.dependencyProject.sourceSets.main
                if (it.dependencyProject.hasProperty("sourceSets.client")) {
                    sourceSet it.dependencyProject.sourceSets.client
                }
            }
        }
    }

    runs {
        client {
            client()
            setConfigName("Quilt Client")
        }
        server {
            server()
            setConfigName("Quilt Server")
        }

        configureEach {
            runDir "../run/${it.configName.replace(' ', '_')}"
            ideConfigGenerated(true)
        }
    }
}

tasks {
    processResources {
        from(sourceSets.main.resources)
        for (p in dependencyProjects) {
            from(p.dependencyProject.sourceSets.main.resources)
            if (p.dependencyProject.hasProperty("sourceSets.client")) {
                from(p.dependencyProject.sourceSets.client.resources)
            }
        }
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    jar {
        from(sourceSets.main.output)
        for (p in dependencyProjects) {
            from(p.dependencyProject.sourceSets.main.output)
            if (p.dependencyProject.hasProperty("sourceSets.client")) {
                from(p.dependencyProject.sourceSets.client.output)
            }
        }
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    sourcesJar {
        from(sourceSets.main.allJava)
        for (p in dependencyProjects) {
            from(p.dependencyProject.sourceSets.main.allJava)
            if (p.hasProperty("sourceSets.client")) {
                from(p.dependencyProject.sourceSets.client.allJava)
            }
        }
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}